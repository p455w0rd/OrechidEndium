buildscript {
    repositories {
        jcenter()
		mavenLocal()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
	dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

group = "p455w0rd.orechidendium"
version = "1.12.2-" + getVersion()
archivesBaseName = "OrechidEndium"
sourceCompatibility = targetCompatibility = "1.8"

minecraft {
	version = "1.12.2-14.23.5.2768"
	mappings = "snapshot_20171003"
	runDir = "run"
	//useDepAts = true
}

repositories {
	maven {
		name "CurseForge"
		url "https://minecraft.curseforge.com/api/maven/"
	}
	maven {
		name "jared"
		url "https://maven.blamejared.com"
	}
	maven {
		name = "ChickenBones"
		url "http://chickenbones.net/maven"
	}
	maven {
		name = "progwml6"
		url = "http://dvs1.progwml6.com/files/maven"
	}
}

dependencies {
	deobfCompile "netherending-ores:Netherending-Ores:1.12.2:1.2"
	deobfCompile "quark:Quark:r1.5:145"
	deobfCompile "mezz.jei:jei_1.12.2:4.14.4.251"
	deobfCompile "baubles:Baubles:1.12:1.5.2"
	deobfCompile "vazkii.botania:Botania:r1.10-359.29"
	deobfCompile "vazkii.autoreglib:AutoRegLib:1.3-27.10"
	deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.11.494"
	deobfCompile "com.blamejared:MTLib:3.0.4.8"
	deobfCompile ("com.brandon3055.brandonscore:BrandonsCore:1.12.2-2.4.9.196:universal") {
		transitive = false
	}
	deobfCompile ("com.brandon3055.draconicevolution:Draconic-Evolution:1.12.2-2.3.18.329:universal") {
		transitive = false
	}
	deobfCompile "codechicken:CodeChickenLib:1.12.2-3.2.2.354:universal"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
		
	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

String getVersion() {
	String major = "0";
	String revision = "0";
	String patch = "0";
	String prefix = "public static final String VERSION = \"";
	File file = file("src/main/java/p455w0rd/orechidendium/init/ModGlobals.java")
	file.eachLine { String s ->
		s = s.trim();
		if (s.startsWith(prefix)) {
			s = s.substring(prefix.length(), s.length() - 2);
			String[] pts = s.split("\\.");

			major = pts[0];
			revision = pts[1];
			patch = pts[2];
		}
	}
	return "$major.$revision.$patch";
}
